plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

Properties loadProperties(String path) {
    def propertiesFile = rootProject.file(path)
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return properties
}

def apiKeyProperties = loadProperties("api-key.properties")

def keystoreProperties = loadProperties("keystore.properties")

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.marvel"
        minSdk 24
        targetSdk 31

        def major = Integer.parseInt(property("version.major"))
        def minor = Integer.parseInt(property("version.minor"))
        def patch = Integer.parseInt(property("version.patch"))
        def buildNumber = Integer.parseInt(property("version.buildNumber"))

        versionCode buildNumber
        versionName "${major}.${minor}.${patch}-b${buildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", "\"https://gateway.marvel.com\""

        buildConfigField("String", "API_KEY_PUBLIC", apiKeyProperties["marvel_api_key.public"])
        buildConfigField("String", "API_KEY_PRIVATE", apiKeyProperties["marvel_api_key.private"])
    }

    signingConfigs {
        production {
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        mock {
            applicationIdSuffix ".mock"
            debuggable true

            buildConfigField "String", "BASE_URL", "\"https://b8d0e3fc-5257-43b6-8a1f-e517b4ae3793.mock.pstmn.io\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.production
        }
    }

    flavorDimensions "environment"
    productFlavors {
        development {
            dimension "environment"
            applicationIdSuffix ".dev"
        }

        production {
            dimension "environment"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def koin_version = "3.1.6"
def nav_version = "2.4.2"

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Koin for Kotlin apps
    implementation "io.insert-koin:koin-android:$koin_version"

    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Okhttp3
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.3")

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    // LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
}